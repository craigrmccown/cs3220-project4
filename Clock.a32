; Addresses for I/O
.NAME	HEX = 0xFFFFF000
.NAME	LEDR = 0xFFFFF020
.NAME	KEY = 0xFFFFF080
.NAME	SW = 0xFFFFF090
.NAME	TCNT = 0xFFFFF100
.NAME	TLIM = 0xFFFF0104
.NAME	TCTRL= 0xFFFF0108

.ORIG 0x100

; T0 is clock value
; T1 is timer limit
; T2 is device addresses
; T3 is current digit
; T4 is digit overflow value
; T5 is timer control bits

XOR		Zero,Zero,Zero      ; Set Zero register to 0
XOR     T0,T0,T0            ; Set current clock value to 0
SW		T0,HEX(Zero)        ; Display 0 on HEX pins
LW      T2,TLimAddr(Zero)   ; Load timer limit address
ADDI	T1,Zero,1000        ; Set timer limit to 1000
SW		T1,0(T2)            ; Store 1000 as the timer limit
LW      T2,TCtrlAddr(Zero)  ; Load timer control address

; Loop until 1 second has passed
ClockLoop:
LW		T5,0(T2)            ; Load current ms
BEQ		T5,T0,ClockLoop     ; Loop unless a second has passed
CALL    IncClock(Zero)      ; Increment clock every second
SW		T0,HEX(Zero)        ; Display clock value on HEX pins
BR      ClockLoop           ; Loop forever

; Increments clock value
IncClock:
ADD		T3,T0,Zero          ; Set current digit to clock value
ANDI	T3,T3,0x0000000F    ; Mask to get least significant digit
ADDI	T4,Zero,0x9         ; Set digit overflow to 9
BEQ		T3,T4,OverOne       ; Go to OverOne if overflow is detected
ADDI	T0,T0,0x1           ; If no overflow, increment clock value
RET

; OverXXX handles clock overflow for each digit
OverOne:
ADD		T3,T0,Zero          ; Set current digit to clock value
ANDI	T3,T3,0x000000F0    ; Mask to get second least significant digit
ADDI	T4,Zero,0x50        ; Set digit overflow to 0x50
BEQ		T3,T4,OverTwo       ; Go to OverTwo if overflow is detected
ADDI	T0,T0,0x10          ; If no overflow, increment clock value by 0x10
ANDI	T0,T0,0xFFFFFFF0    ; Set least significant digit to 0
RET

; The rest of the OverXXX sections follow the same pattern
OverTwo:
ADD		T3,T0,Zero
ANDI	T3,T3,0x00000F00
ADDI	T4,Zero,0x900
BEQ		T3,T4,OverThree
ADDI	T0,T0,0x100
ANDI	T0,T0,0xFFFFFF00
RET

OverThree:
ADD		T3,T0,Zero
ANDI	T3,T3,0x0000F000
ADDI	T4,Zero,0x5000
BEQ		T3,T4,OverFour
ADDI	T0,T0,0x1000
ANDI	T0,T0,0xFFFFF000
RET

; First check if hours have reached 23, then check if
; the fifth digit has overflowed
OverFour:
ADD		T3,T0,Zero
ANDI    T3,T3,0x00FF0000
ADDI    T4,Zero,0x230000
BEQ     T3,T4,Rollover
ANDI	T3,T3,0x000F0000 
ADDI	T4,Zero,0x90000
BEQ		T3,T4,OverFive
ADDI	T0,T0,0x10000
ANDI	T0,T0,0xFFFF0000
RET

; Just increment the sixth digit
OverFive:
ADDI	T0,T0,0x100000
ANDI	T0,T0,0xFFF00000
RET

; Roll back over to 0
Rollover:
ANDI	T0,T0,0xFF000000
RET

TLimAddr:
.WORD TLIM

TCtrlAddr:
.WORD TCTRL
